
<header>
    <nav>
      <ul>
        <li><a href="lab1.html">lab1 - wyświetlanie </a></li>
        <li><a href="lab2.html">lab2 - wyświetlanie w 16-tkowym</a></li>
        <li><a href="lab3.html">lab3 - sortowanie tablicy int</a></li>
        <li><a href="lab4.html">lab4 - brak </a></li>
        <li><a href="lab5.html">lab5 - zamiana liter małych na duże i odwrotnie </a></li>

        <li><a href="lab6a.html">lab6a - silnia </a></li>
		<li><a href="lab6b.html">lab6b - fibonaci </a></li>

		<li><a href="lab7a.html">lab7a - silnia nie rekurencyjnie </a></li>
		<li><a href="lab7b.html">lab7b - fib nie rekurencyjnie </a></li>
		<li><a href="lab7d.html">lab7d - budowanie stringa </a></li>

        <li><a href="lab8a.html">lab8a - wyświetlanie zmiennych środowiskowych</a></li>


        <li><a href="lab9a.html">lab9a - operacje na zmiennoprzecinkowych </a></li>
        <li><a href="lab9b.html">lab9b - pierwiastki równania kwadratowego </a></li>

        <li><a href="jk1.html">jk1 - maksymalna różnica max_diff(ina, int b, int c, int d) </a></li>
        <li><a href="jk2.html">jk2 - generuj string generate_str(char* s, int c, int n, int inc) </a></li>
        <li><a href="jk3.html">jk3 - łączenie stringów w jeden char* fun(char* buf, char* a, char* b)</a></li>
        <li><a href="jk4.html">jk4 - max index max_ind(int a, int b, int c, int d)</a></li>
		<li><a href="jk5.html">jk5 - (poprawkowy) dziwny max na longach max_1_ind(long long *tab, long long n, long long *even_count, long long *neg_count)</a></li>




        <li><a href="pw1.html">pw1 - maksymalna różnica max_diff(ina, int b, int c, int d) </a></li>
        <li><a href="ds2.html">ds2 - generuj string generate_str(char* s, int c, int n, int inc) </a></li>
        <li><a href="ds3.html">ds3 - łączenie stringów w jeden char* fun(char* buf, char* a, char* b)</a></li>
        <li><a href="ds4.html">ds4 - max index max_ind(int a, int b, int c, int d)</a></li>
		<li><a href="ds5.html">ds5 - (poprawkowy) dziwny max na longach max_1_ind(long long *tab, long long n, long long *even_count, long long *neg_count)</a></li>



      </ul>
    </nav>
</header>


<textarea style="width:2000px; height:10000px;" >
#----------------------------------------------------------------
# Program LAB_3.S - Asemblery Laboratorium IS II rok
#----------------------------------------------------------------
#
#  To compile: as -o lab_3.o lab_3.s
#  To link:    ld -o lab_3 lab_3.o
#  To run:     ./lab_3
#
#---------------------------------------------------------------- 

	.equ	write_64,	1
	.equ	exit_64,	60
	.equ	stdout,		1

	.equ	dig0,		'0'

#----------------------------------------------------------------

	.data

table:					# table of items
	.long	10,70,50,90,60,80,40,20,0,30, 223
count:					# count of items
	.quad	( . - table ) >> 2
events:	
	.quad	0
item:	
	.string	"Item "
line_no:	
	.string	"   "
itemval:	
	.string	" = "
number:	
	.string	"     \n"
before:	
	.string	"\nBefore:\n"
after:	
	.string	"\nAfter:\n"
dataend:

	.equ	item_len, before - item
	.equ	bef_len, after - before
	.equ	aft_len, dataend - after

#----------------------------------------------------------------

	.text
	.global _start

_start:
	MOV	$write_64,%rax	# display message
	MOV	$stdout,%rdi
	MOV	$before,%rsi
	MOV	$bef_len,%rdx
	SYSCALL

	CALL	disp_table	# display content of table

	CALL	do_something	# do something with table

	MOV	$write_64,%rax	# display message
	MOV	$stdout,%rdi
	MOV	$after,%rsi
	MOV	$aft_len,%rdx
	SYSCALL

	CALL	disp_table	# display content of table

	MOV	events,%rdi	# exit program
	MOV	$exit_64,%rax
	SYSCALL

#----------------------------------------------------------------
#
#	Function:	do_something
#	Parameters:	none
#

	.type do_something,@function

do_something:
	MOV	count,%rdx		# outer loop counter
	MOVQ	$0,events
outer:		
	DEC	%rdx
	XOR	%rsi,%rsi		# data index
	MOV	%rdx,%rcx		# inner loop counter
inner:		
	MOV	table(,%rsi,4),%eax
	CMP	table+4(,%rsi,4),%eax
	JBE	noswap
	XCHG	table+4(,%rsi,4),%eax
	MOV	%eax,table(,%rsi,4)
	INCQ	events
noswap:		
	INC	%rsi			# next element
	LOOP	inner			# { rcx--; if( rcx ) goto inner }
	CMP	$1,%rdx
	JNZ	outer

	RET

#----------------------------------------------------------------
#
#	Function:	disp_table
#	Parameters:	none
#

	.type disp_table,@function

disp_table:
	XOR	%rsi,%rsi		# data index
	MOV	count,%rcx		# data count

disp_item:
	
	
	MOV	table(,%rsi,4),%ebx	# get data
	CALL	make_string		# convert to string
	
	PUSH    %rsi
	PUSH    %rcx
	MOV	$write_64,%rax		# call write function
	MOV	$stdout,%rdi
	MOV	$item,%rsi
	MOV	$item_len,%rdx
	SYSCALL

	POP     %rcx	
	POP     %rsi


	INC	%rsi			# next element
	LOOP	disp_item		# { rcx--; if( rcx ) goto disp_item }

	RET				# return to main program

#----------------------------------------------------------------
#
#	Function:	make_string
#	Parameters:	%esi - index of element
#			%ebx - value of element
#

	.type make_string,@function

make_string:
	
	MOVW	$0x20202020, line_no
	MOVL	$0x20202020, number
	MOV	%esi,%eax		# convert index of table element to string
	MOV	$line_no + 2,%rdi
	CALL	n2str

	MOV	%ebx,%eax		# convert value of table element to string
	MOV	$number + 4,%rdi
	CALL	n2str

	RET				# return to disp_table function

#----------------------------------------------------------------
#
#	Function:	n2str
#	Parameters:	%eax - value
#			%rdi - address of last character
#

	.type n2str,@function
n2str:
	PUSH	%rbx		# save register on stack
	PUSH	%rdx		# save register on stack
	MOV	$10,%ebx	# divisor in EBX, dividend in EAX
nextdig:			
	XOR	%edx,%edx	# EDX = 0
	DIV	%ebx		# EDX:EAX div EBX
	ADD	$dig0,%dl	# convert remainder (in EDX) to character
	MOV	%dl,(%rdi)	# *(RDI) = character (decimal digit)
	CMP	$0,%eax		# quotient in EAX 
	JZ	empty
	DEC	%rdi		# RDI--
	JMP	nextdig		
empty:		
	POP	%rdx		# restore register from stack
	POP	%rbx		# restore register from stack

	RET			# return to make_string function

#----------------------------------------------------------------
</textarea>