<header>
    <nav>
      <ul>
        <li><a href="lab1.html">lab1 - wyświetlanie </a></li>
        <li><a href="lab2.html">lab2 - wyświetlanie w 16-tkowym</a></li>
        <li><a href="lab3.html">lab3 - sortowanie tablicy int</a></li>
        <li><a href="lab4.html">lab4 - brak </a></li>
        <li><a href="lab5.html">lab5 - zamiana liter małych na duże i odwrotnie </a></li>

        <li><a href="lab6a.html">lab6a - silnia </a></li>
		<li><a href="lab6b.html">lab6b - fibonaci </a></li>

		<li><a href="lab7a.html">lab7a - silnia nie rekurencyjnie </a></li>
		<li><a href="lab7b.html">lab7b - fib nie rekurencyjnie </a></li>
		<li><a href="lab7d.html">lab7d - budowanie stringa </a></li>

        <li><a href="lab8a.html">lab8a - wyświetlanie zmiennych środowiskowych</a></li>


        <li><a href="lab9a.html">lab9a - operacje na zmiennoprzecinkowych </a></li>
        <li><a href="lab9b.html">lab9b - pierwiastki równania kwadratowego </a></li>

        <li><a href="jk1.html">jk1 - maksymalna różnica max_diff(ina, int b, int c, int d) </a></li>
        <li><a href="jk2.html">jk2 - generuj string generate_str(char* s, int c, int n, int inc) </a></li>
        <li><a href="jk3.html">jk3 - łączenie stringów w jeden char* fun(char* buf, char* a, char* b)</a></li>
        <li><a href="jk4.html">jk4 - max index max_ind(int a, int b, int c, int d)</a></li>
		<li><a href="jk5.html">jk5 - (poprawkowy) dziwny max na longach max_1_ind(long long *tab, long long n, long long *even_count, long long *neg_count)</a></li>




        <li><a href="pw1.html">pw1 - maksymalna różnica max_diff(ina, int b, int c, int d) </a></li>
        <li><a href="ds2.html">ds2 - generuj string generate_str(char* s, int c, int n, int inc) </a></li>
        <li><a href="ds3.html">ds3 - łączenie stringów w jeden char* fun(char* buf, char* a, char* b)</a></li>
        <li><a href="ds4.html">ds4 - max index max_ind(int a, int b, int c, int d)</a></li>
		<li><a href="ds5.html">ds5 - (poprawkowy) dziwny max na longach max_1_ind(long long *tab, long long n, long long *even_count, long long *neg_count)</a></li>



      </ul>
    </nav>
</header>
<textarea style="width:2000px; height:10000px;" >
#----------------------------------------------------------------
# Program lab_9a.s - Asemblery Laboratorium IS II rok
#----------------------------------------------------------------
#
#   to compile & link:  gcc -lm -o lab_9a lab_9a.s
#   to run: 		./lab_9a
#
#----------------------------------------------------------------

	.data
i:				# loop counter
	.long		1
it:				# TODO
	.long		1
x:				# function argument
	.double		0.0
y:
	.double		0.0
sqr_a:				# function result
	.double		0.0
sqr_b:				# function result
	.double		0.0
two:				# constant
	.long		2
fmt_str:
	.asciz	"Square root of %lf = %.20lf\n"

	
	
	# x(k+1)  = 1/2*(xk + a/xk)
	#sufixy S-short, L-long, 
	.text
	.global main
	
main:
	FINIT			# FPU initialization
next:
	FILDL	i		# i -> ST(0)
	FSTPL	x		# ST(0) -> x & pop from stack
				#------------------------------
	FLDL	x		# function argument -> ST(0)
	FSQRT			# sqrt( ST(0) ) -> ST(0)
	FSTPL	sqr_a		# ST(0) -> sqr_a  & pop from stack
				#------------------------------
	FLDL	sqr_a		# load & display first result
	CALL	disp
				#------------------------------
	FLDL	x		# first approximation (a0) -> ST(0)

iter:	FLDL	x		# function argument -> ST(0), ak in ST(1)
	FDIV	%ST(1), %ST(0)	# ST(0)/ST(1) -> ST(0)    x/ak
	FADD	%ST(1), %ST(0)	# ST(0)+ST(1) -> ST(0)    ak+x/ak
	FIDIVL	two		# ST(0)/two -> ST(0)      (ak+x/ak)/2
	FCOMI	%ST(1)		# ST(1) ? ST(0)           ak ? ak+1
	FSTP	%ST(1)		# ST(0) -> ST(1) & pop from stack
	JNZ	iter		# test of convergence

	FSTPL	sqr_b		# ST(0) -> sqr_b & pop from stack
				#------------------------------
	FLDL	sqr_b		# load & display second result
	CALL	disp

	INCL	i		# next argument
	CMPL	$10, i		# enough ?
	JBE	next
				#------------------------------
	mov $0, %rdi		# the end
	CALL	exit
	

	.type	disp, @function	# printf( fmt_str, x, ST(0) )
disp:
	FSTPL 	y		# store number in memory
	movq	x, %xmm0
	movq	y, %xmm1
	mov	$fmt_str, %rdi	# address of fmt_str
	mov $2, %al
	CALL	printf
	RET
</textarea>